buildscript {
    repositories {
        jcenter()
        maven { url "https://dl.bintray.com/eaglesakura/maven/" }
    }
    dependencies {
        classpath "com.eaglesakura:sloth-plugin:1.0.9"
    }
}
apply plugin: 'com.eaglesakura.android-mipmap' /* Generate Android Mipmap */
apply plugin: 'com.eaglesakura.sloth.prop' /* Generate App Properties */

/**
 * mipmap生成管理
 *
 * ImageMagickを導入し、環境変数下記コマンドのパスを設定する
 * $IMGMAGICK_CONVERT   convertコマンド
 * $IMGMAGICK_IDENTIFY  identifyコマンド
 */
mipmap {
    sources = file("image/");
    output = file("src/main/res");
    toolsPath.identify = System.getenv("IMGMAGICK_IDENTIFY");
    toolsPath.convert = System.getenv("IMGMAGICK_CONVERT");
    cleanOldFiles = false;
}

/**
 * アプリのプロパティ管理
 */
props {
    output file("src/main/gen/java")
    property file("config/app_properties.json"), "replace.your.app_package.gen.prop"
}

/**
 * Firebaseで管理されるConfigを生成する
 */
task generateFirebaseConfig(type: com.eaglesakura.sloth.firebase.FirebaseRemoteConfigGenTask) {
    outDirectory = file("src/main/gen/java")
    def AppStatusConfig = newConfig("${android.defaultConfig.applicationId}.gen.config.AppStatusConfig");
    AppStatusConfig.stringConfig("stringConfig", "nil");        // 文字列値
    AppStatusConfig.doubleConfig("doubleConfig", 0);            // double値
    AppStatusConfig.longConfig("longConfig", 0);                // long値
    AppStatusConfig.intConfig("intConfig", 0);                  // int値
    AppStatusConfig.booleanConfig("booleanConfig", false);      // bool値
}

/**
 * greenDAOのData Access Object(HogeDao/DaoMaster)を生成する
 */
task generateDao(type: com.eaglesakura.sloth.dao.AndroidDaoGenTask) {
    outDirectory = file("src/main/gen/java")
    classPackageBase = "${android.defaultConfig.applicationId}.gen.dao"

    def schema = newSchema(1, "example");

    /**
     * 表示スロットと表示内容(対象プラグイン、表示ID)を紐付ける
     *
     * packageName + slotIdで一意に識別される
     */
    def DbExample = schema.addEntity("DbExample");
    DbExample.addStringProperty("uniqueId").notNull().unique().index().primaryKey();
    DbExample.addDateProperty("updatedDate").notNull();    // 更新日時
}
